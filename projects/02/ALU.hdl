// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs,
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Mux16(a[0..15]=x, b[0..15]=false, sel=zx, out[0..15]=x1);
    Mux16(a[0..15]=y, b[0..15]=false, sel=zy, out[0..15]=y1);

    Not16(in[0..15]=x1, out[0..15]=xcomp);
    Mux16(a[0..15]=x1, b[0..15]=xcomp, sel=nx, out[0..15]=x2);

    Not16(in[0..15]=y1, out[0..15]=ycomp);
    Mux16(a[0..15]=y1, b[0..15]=ycomp, sel=ny, out[0..15]=y2);

    And16(a[0..15]=x2, b[0..15]=y2, out[0..15]=xandy);
    Add16(a[0..15]=x2, b[0..15]=y2, out[0..15]=xaddy);
    Mux16(a[0..15]=xandy, b[0..15]=xaddy, sel=f, out[0..15]=out1);

    Not16(in[0..15]=out1, out[0..15]=outcomp);
    Mux16(a[0..15]=out1, b[0..15]=outcomp, sel=no, out[0..15]=out, out[0..15]=out2, out[15]=ng, out[15]=ng1);

    Add16(a[0..15]=out2, b[0..15]=true, out[0..15]=out3, out[15]=out315);
    Not(in=ng1, out=ngcomp);
    And(a=ngcomp, b=out315, out=zr);
}
